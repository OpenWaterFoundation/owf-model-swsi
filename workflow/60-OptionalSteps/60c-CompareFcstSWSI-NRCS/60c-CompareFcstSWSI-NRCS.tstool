# Read the start and end dates for the recent+current period
ReadPropertiesFromExcel(InputFile="..\..\CO-SWSI-Control.xlsx",Worksheet="Config",PropertyCellMap="RecentPeriodStartDateText:*,CurrentMonthDateText:*,NumberOfHUCs:*")
#
# Read combined inputs for HUC identifiers
ReadTableFromExcel(TableID="Combined Inputs",InputFile="..\..\CO-SWSI-Control.xlsx",Worksheet="Combined Inputs",ExcelColumnNames=FirstRowInRange,ColumnExcludeFilters="HUC:",Comment="NO")
# Create list of unique HUCs for looping
# CopyTable(TableID="Combined Inputs",NewTableID="HUCList_Unique",IncludeColumns="HUC, HUC Name",DistinctColumns="HUC, HUC Name",ColumnFilters="HUC:13010001",RowCountProperty="HUCCount")
CopyTable(TableID="Combined Inputs",NewTableID="HUCList_Unique",IncludeColumns="HUC, HUC Name",DistinctColumns="HUC, HUC Name",RowCountProperty="HUCCount")
# -- Check that number of HUCs matched expected number.
If(Name="CheckHUCCount",Condition="${HUCCount}!=${NumberOfHUCs}")
Message(Message="ERROR - ${HUCCount} HUCs found but ${NumberOfHUCs} expected. Check HUC IDs and HUC Names in control file.",CommandStatus=FAILURE)
Exit()
EndIf(Name="CheckHUCCount")
##################################
# Forecast SWSI Comparison
##################################
SetInputPeriod(InputStart="${RecentPeriodStartDateText}",InputEnd="${CurrentMonthDateText}")
# Read the NRCS forecast SWSI time series
ReadDateValue(InputFile="..\..\Input-TimeSeries-ForSWSI\ComparisonData\NRCS-SWSI-Recent.dv",Alias="%L-NRCS-Forecast-SWSI")
# Read the CO SWSI forecast SWSI time series. These results must be generated using RecentPeriodFlowDataType=ForecastedNaturalFlow
ReadDateValue(InputFile="..\..\Results-Web\swsi-by-huc\SWSI-TimeSeries.dv")
# Start HUC Loop
For(Name="HUCLoop",IteratorProperty="HUCID",TableID="HUCList_Unique",TableColumn="HUC")
SetPropertyFromTable(TableID="HUCList_Unique",Column="HUC Name",ColumnIncludeFilters="HUC:${HUCID}",PropertyName="hucName",DefaultValue="Not Working!")
#
RemoveFile(InputFile="../../Results-Web/graphs-png/FCST-SWSI-COMPARE/HUC-${HUCID}-Forecast-SWSI-comparison-graph.png",IfNotFound=Ignore)
RemoveFile(InputFile="../../Results-Web/graphs-tsp/FCST-SWSI-COMPARE/HUC-${HUCID}-Forecast-SWSI-comparison-graph.tsp",IfNotFound=Ignore)
ExpandTemplateFile(InputFile="Compare-Forecast-SWSI-graph-template.tsp",OutputFile="..\..\Results-Web\graphs-tsp\FCST-SWSI-COMPARE\HUC-${HUCID}-Forecast-SWSI-comparison-graph.tsp",ListInResults=False)
ProcessTSProduct(TSProductFile="..\..\Results-Web\graphs-tsp\FCST-SWSI-COMPARE\HUC-${HUCID}-Forecast-SWSI-comparison-graph.tsp",View=False,OutputFile="../../Results-Web/graphs-png/FCST-SWSI-COMPARE/HUC-${HUCID}-Forecast-SWSI-comparison-graph.png",VisibleStart="${RecentPeriodStartDateText}",VisibleEnd="${CurrentMonthDateText}")
EndFor(Name="HUCLoop")
Exit()
